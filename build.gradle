plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
        resources {
            srcDirs += [
                    'src/testmod/generated'
            ]
        }
    }
}

base {
    archivesName = project.archives_base_name
}

loom {
    accessWidenerPath = file("src/main/resources/zine.accesswidener")
    runs {
        testmodClient {
            client()
            name = "Testmod Client"
            source project.sourceSets.testmod
        }
    }
    mods {
        'zine' {
            sourceSet('main')
        }
        'zinetest' {
            sourceSet('testmod')
        }
    }

}

fabricApi {
    configureDataGeneration {
        client = true
        outputDirectory = file("src/testmod/generated")
        addToResources = false
        strictValidation = true
    }
}

runDatagen {
    outputs.dir("src/testmod/generated")
}

repositories {
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    afterEvaluate {
        testmodImplementation sourceSets.main.output
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "class_2068": "\$class_2068",
                "class_9700": "\$class_9700",
                "class_10431": "\$class_10431",
                "class_10436": "\$class_10436",
                "class_10438": "\$class_10438",
                "class_10450": "\$class_10450",
                "class_10453": "\$class_10453",
                "class_10456": "\$class_10456"
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
    repositories {
    }
}
